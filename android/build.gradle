import groovy.json.JsonSlurper

def computeVersionName() {
    // dynamically retrieve version from package.json
    def slurper = new JsonSlurper()
    def json = slurper.parse(file('../../../package.json'), "utf-8")
    return json.version
}
def computeQQAppId() {
    // dynamically retrieve qq app id from package.json
    def slurper = new JsonSlurper()
    def json = slurper.parse(file('../../../package.json'), "utf-8")
    return json.qq_app_id
}
apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION   = 28
def DEFAULT_BUILD_TOOLS_VERSION   = "28.0.3"
def DEFAULT_TARGET_SDK_VERSION    = 28
def DEFAULT_MIN_SDK_VERSION       = 16

android {
    compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
    buildToolsVersion rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion rootProject.hasProperty('minSdkVersion') ? rootProject.minSdkVersion : DEFAULT_MIN_SDK_VERSION
        targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION

        versionCode 1
        versionName computeVersionName()
        manifestPlaceholders = [
                QQ_APP_ID: computeQQAppId()
        ]
    }
    sourceSets.main {
        jniLibs.srcDirs = ['libs']
    }
}
repositories {
    mavenCentral()
}

dependencies {
    implementation "com.facebook.react:react-native:+"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}